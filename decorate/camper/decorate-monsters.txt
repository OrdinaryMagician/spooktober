ACTOR WaterDoomImp 12125
{
	//$Category SpookyMonsters
	Health 100
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 200
	Monster
	bloodcolor 252 224 24
	+FLOORCLIP
	+NOPAIN
	PainSound "imp/pain"
	DeathSound "imp/death"
	ActiveSound "imp/active"
	HitObituary "$OB_IMPHIT"
	Obituary "$OB_IMP"
	States
	{
		Spawn:
			WIMP ABCD 10 A_Look
			Loop
		See:
			WIMP AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			WIMP EF 8 A_FaceTarget
			WIMP G 6 A_CustomComboAttack("WaterDoomImpBall", 32, 3 * random(1, 8), "imp/melee")
			Goto See
		Pain:
			WIMP H 2
			WIMP H 2 A_Pain
			Goto See
		Death:
			WIMP I 8
			WIMP J 8 A_Scream
			WIMP K 6
			WIMP L 6 A_NoBlocking
			WIMP M -1
			Stop
		XDeath:
			TROO N 5
			TROO O 5 A_XScream
			TROO P 5
			TROO Q 5 A_NoBlocking
			TROO RST 5
			TROO U -1
			Stop
		Raise:
			WIMP ML 8
			WIMP KJI 6
			Goto See
	}
}

ACTOR WaterDoomImpBall
{
	Radius 6
	Height 8
	Speed 10
	FastSpeed 20
	Damage 3
	Projectile
	+RANDOMIZE
	Translation "0:255=%[0.00,0.00,0.00]:[1.25,2.00,0.50]"
	RenderStyle Add
	Alpha 1
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	States
	{
		Spawn:
			BAL1 AB 4 Bright
			Loop
		Death:
			BAL1 CDE 6 Bright
			Stop
	}
}

ACTOR WaterDoomImpTrap 12126
{
	//$Category SpookyMonsters
	Health 50
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 200
	bloodcolor 252 224 24
	Monster
	+FLOORCLIP
	+NOPAIN
	SeeSound "imp/sight"
	HitObituary "$OB_IMPHIT"
	Obituary "$OB_IMP"
	States
	{
		Spawn:
			WIMB P 1 A_LookEx(0,0,128,256,360,"See")
			Loop
		See:
			WIMB P 1 A_AlertMonsters(128)
			WIMB PRSTU 4 A_FaceTarget
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnItem("WaterDoomImp",0,0,0,1)
			Stop
		Death:
			WIMB A 8
			WIMB B 8 A_Scream
			WIMB C 6
			WIMB D 6 A_NoBlocking
			WIMB E -1
			Stop
	}
}

ACTOR WaterDoomImpTrapDisabled 12127
{
	//$Category SpookyMonsters
	Radius 20
	Height 56
	-SOLID
	States
	{
		Spawn:
			WIMB P -1
			Stop
	}
}

actor Aracnorb 12128
{
	//$Category SpookyMonsters
	MONSTER
	+NOGRAVITY
	+FLOAT
	+FLOATBOB
	Health 200
	Radius 24
	Height 56
	Mass 400
	Speed 12
	PainChance 150
	bloodcolor 252 224 24
	SeeSound "aracnorb/sight"
	ActiveSound "baby/active"
	PainSound "baby/pain"
	DeathSound "aracnorb/death"
	MeleeSound "aracnorb/melee"
	MeleeDamage 6
	MissileHeight 36
	MissileType AracnorbBall
	Obituary "%o suffered psychic trauma from an aracnorb's brainwaves."
	HitObituary "%o had %p skull chewed by an aracnorb."
	Scale 0.85
	States
	{
		Spawn:
			ACNB A 1 A_Look
			Loop
		See:
			ACNB A 2 A_Chase
			Loop
		Melee:
			ACNB AB 5
			ACNB C 6 A_MeleeAttack
			Goto See
		Missile:
			ACNB B 12 Bright A_FaceTarget
			ACNB C 2 Bright A_MissileAttack
			ACNB B 2 Bright
			ACNB D 0 A_Jump(32,"See")
			ACNB D 0 A_SpidRefire
			Goto Missile+1
		Pain:
			ACNF I 2
			ACNF I 2 A_Pain
			Goto See
		Death:
			ACNB D 0 A_ChangeFlag("FLOATBOB",0)
			ACNB D 0 A_Scream
			ACNB D 6 A_Fall
			ACNB D 1
			Wait
		Crash:
			ACNB EFG 6
			ACNB H -1
			Stop
		Raise:
			ACNB HGFEDA 8
			ACNB A 0 A_ChangeFlag("FLOATBOB",1)
			Goto See
	}
}

actor AracnorbBall
{
	Radius 13
	Height 8
	Speed 8
	Damage 3
	RenderStyle Add
	Alpha 0.75
	SeeSound "baby/attack"
	DeathSound "baby/shotx"
	PROJECTILE
	+STRIFEDAMAGE
	+SEEKERMISSILE
	+RANDOMIZE
	States
	{
		Spawn:
			ACNF AABB 1 Bright A_BishopMissileWeave
			Loop
		Death:
			ACNF CDEFG 5 Bright
			Stop
	}
}



Actor Wicked 30133
{
	//$Category SpookyMonsters
	Health 300
	Radius 25
	Height 88
	Mass 200
	Speed 10
	PainChance 112
	Monster
	bloodcolor 67 0 202
	+NoGravity
	+Float
	Obituary "%o got blasted by a Wicked."
	SeeSound "monster/wiksit"
	PainSound "monster/wikpai"
	DeathSound "monster/wikdth"
	ActiveSound "monster/wikact"
	States
	{
		Spawn:
			WICK ABCD 8 A_Look
			Loop
		See:
			WICK AABBCCDD 4 A_Chase
			WICK A 0 A_Jump(64,1)
			Goto See
			WICK A 0
			Goto See2
			See2:
			WICK EEFFGGHH 2 A_Chase
			WICK E 0 A_Jump(32,1)
			Goto See2
			WICK E 0
			Goto See
		Missile:
			WICK I 1 A_PlaySound("monster/wikatk")
			WICK I 4 A_FaceTarget
			WICK J 5 BRIGHT A_FaceTarget
			WICK K 8 BRIGHT A_CustomMissile("WickedShot",60,0,0)
			WICK L 5
			Goto See
		Pain:
			WICK M 3
			WICK M 3 A_Pain
			WICK M 3
			Goto See
		Death:
			WICK N 5 A_Scream
			WICK OP 5
			WICK P 0 A_SpawnItemEX("WickedTorso",0,0,48,0,0,0,0,16)
			WICT A 5 A_NoBlocking
			WICT BCDEF 5
			WICT G -1 A_SetFloorClip
			Stop
		Raise:
			WICT G 0 A_RemoveChildren(TRUE)
			WICK G 0 A_SpawnItemEx("WickedTorsoRes",0,0,0,0,0,1,0,0)
			WICT G 5 A_UnSetFloorClip
			WICT FEDBC 5
			WICK PON 5
			Goto See
	}
}

Actor WickedShot
{
	Radius 8
	Height 8
	Speed 10
	Damage 5
	RenderStyle Add
	DamageType Fire
	Alpha 0.67
	Projectile
	+ThruGhost
	+SeekerMissile
	+ForceXYBillboard
	SeeSound "weapons/firmfi"
	DeathSound "weapons/firex5"
	Decal "DoomImpScorch"
	States
	{
		Spawn:
			WIBL ABCDABCDABCDABCDABCDABCDABCD 1 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
			WIBL ABCD 0 Bright A_SeekerMissile(90,90)
			Loop
		Death:
			WIBL JKLMN 3 Bright
			Stop
	}
}

Actor WickedFX
{
	Radius 2
	Height 2
	+NoBlockMap
	+NoGravity
	+DontSplash
	+ForceXYBillboard
	RenderStyle Add
	Alpha 0.67
	States
	{
		Spawn:
			TNT1 A 3 Bright
			WIBL EFGHI 3 BRIGHT
			Stop
	}
}

Actor WickedTorso
{
	Mass 1000000
	Radius 1
	Height 1
	+IsMonster
	+Corpse
	States
	{
		Spawn:
			TNT1 A 0 // Without this, the sound below will never be played
			WICK Q 5 A_PlaySound("monster/tenpn1")
			WICK R 5
			Wait
		Crash:
			WICK S 1 A_SetFloorClip
			WICK S 4 A_PlaySound("monster/tenpn2")
			WICK TUV 5
			WICK W -1
			Stop
	}
}

Actor WickedTorsoRes
{
	Radius 24
	Height 24
	+NoGravity
	+NoBlockMap
	+NoClip
	States
	{
		Spawn:
			WICK WVUTSRQ 5
			Stop
	}
}

Actor Fleshspawn 12187
{
	//$Category SpookyMonsters
	HitObituary "%o was devoured by a Flesh Spawn."
	health 5
	radius 14
	height 30
	mass 80
	speed 10
	damage 5
	painchance 224
	bloodcolor "blue"
	attacksound "flesh/melee"
	seesound "flesh/sight"
	painsound "flesh/pain"
	deathsound "flesh/death"
	activesound "flesh/active"
	MONSTER
	+FLOAT
	+NOGRAVITY
	+DONTHURTSPECIES
	+NOINFIGHTING
	+NOTARGET
	States
	{
		Spawn:
			FLSP CD 8 A_Look
			loop
		See:
			FLSP AABB 2 A_Chase
			loop
		Missile:
			FLSP BA 2 A_FaceTarget
			FLSP E 4 A_SkullAttack
			FLSP ABD 1
			goto See
		Pain:
			FLSP I 3
			FLSP I 3 A_Pain
			Goto See
		Death:
			FLSP I 2
			FLSP J 2 A_Scream
			FLSP K 2
			FLSP L 2 A_NoBlocking
			FLSP M 2
			TNT1 A -1
			Stop
	}
}