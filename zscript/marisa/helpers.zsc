// Useful shared code
Class SpooktoberUtil abstract play
{
	// sets a lineid's light level (ACS doesn't have a function for this)
	static void SetLineLight( int tag, int brightness, bool absolute = false )
	{
		let l = LineIdIterator.Create(tag);
		int i;
		while ( (i = l.Next()) != -1 )
		{
			for ( int j=0; j<2; j++ )
			{
				if ( !level.lines[i].sidedef[j] ) continue;
				level.lines[i].sidedef[0].light = brightness;
				if ( absolute ) level.lines[i].sidedef[0].flags |= Side.WALLF_ABSLIGHTING;
				else level.lines[i].sidedef[0].flags &= ~Side.WALLF_ABSLIGHTING;
			}
		}
	}

	static void SetDaylight( int tag )
	{
		let ai = ActorIterator.Create(tag);
		Actor a;
		while ( a = ai.Next() )
		{
			if ( (a.args[0] == 255) && (a.args[1] == 160) && (a.args[2] == 32) )
				continue;
			a.args[0] = 255;
			a.args[1] = 160;
			a.args[2] = 32;
			a.args[3] *= 2; // this part can't be done in acs
		}
	}

	// marks lines as seen/unseen in automap
	static void ShowLine( int tag, bool show = true, bool noforce = false )
	{
		let l = LineIdIterator.Create(tag);
		int i;
		while ( (i = l.Next()) != -1 )
		{
			if ( show )
			{
				level.lines[i].flags &= ~Line.ML_DONTDRAW;
				if ( noforce ) continue;
				level.lines[i].flags |= Line.ML_MAPPED;
				level.lines[i].validcount = 1;
			}
			else
			{
				level.lines[i].flags |= Line.ML_DONTDRAW;
				if ( noforce ) continue;
				level.lines[i].flags &= ~Line.ML_MAPPED;
				level.lines[i].validcount = 0;
			}
		}
	}

	static void SetSectorPlaneLight( int tag, int part, int brightness )
	{
		let s = SectorTagIterator.Create(tag);
		int i;
		while ( (i = s.Next()) != -1 )
			level.sectors[i].SetPlaneLight(part,brightness);
	}

	private static bool NotReallyObstructing( Vector3 testpos, Actor a, Actor b )
	{
		Vector3 vdiff = level.Vec3Diff(testpos,b.pos);
		if ( abs(vdiff.x) > (a.radius+b.radius) ) return true;
		if ( abs(vdiff.y) > (a.radius+b.radius) ) return true;
		if ( (vdiff.z > 0) && (abs(vdiff.z) > a.height) ) return true;
		if ( (vdiff.z < 0) && (abs(vdiff.z) > b.height) ) return true;
		return false;
	}

	// door transition teleport for hub
	static bool DoorMove( Actor whom, int angle, bool checkonly = false )
	{
		if ( !whom ) return true;
		Vector2 movedir = Actor.RotateVector((128,0),angle);
		Vector3 ofspos = whom.Vec3Offset(movedir.x,movedir.y,0);
		let bt = BlockThingsIterator.CreateFromPos(ofspos.x,ofspos.y,ofspos.z,whom.height,whom.radius,true);
		while ( bt.Next() )
		{
			let t = bt.Thing;
			if ( !t || !t.bSOLID || (t == whom) || NotReallyObstructing(ofspos,whom,t) ) continue;
			return false;
		}
		if ( checkonly ) return true;
		whom.SetOrigin(ofspos,false);
		whom.angle = angle;
		whom.pitch = 0;
		return true;
	}

	// spot transition teleport for secret03
	static bool SpotMove( Actor whom, int spot )
	{
		if ( !whom ) return true;
		Actor a = ActorIterator.Create(spot).Next();
		if ( !a ) return true;
		Vector3 destpos = a.pos;
		let bt = BlockThingsIterator.CreateFromPos(destpos.x,destpos.y,destpos.z,whom.height,whom.radius,true);
		while ( bt.Next() )
		{
			let t = bt.Thing;
			if ( !t || !t.bSOLID || (t == whom) || NotReallyObstructing(destpos,whom,t) ) continue;
			return false;
		}
		whom.SetOrigin(destpos,false);
		whom.angle = a.angle;
		whom.pitch = 0;
		return true;
	}

	// adds totallyfrozen and nogravity, clears velocity
	static void FullFreeze( Actor whom )
	{
		if ( !whom || !whom.player )
		{
			for ( int i=0; i<MAXPLAYERS; i++ )
			{
				if ( !playeringame[i] ) continue;
				players[i].cheats |= CF_TOTALLYFROZEN;
				players[i].vel *= 0;
				players[i].mo.vel *= 0;
				players[i].mo.PlayIdle();
				players[i].mo.bNODAMAGE = true;
			}
			return;
		}
		whom.player.cheats |= CF_TOTALLYFROZEN;
		whom.player.vel *= 0;
		whom.vel *= 0;
		PlayerPawn(whom).PlayIdle();
		whom.bNODAMAGE = true;
	}

	// removes totallyfrozen and nogravity
	static void FullUnfreeze( Actor whom )
	{
		if ( !whom || !whom.player )
		{
			for ( int i=0; i<MAXPLAYERS; i++ )
			{
				if ( !playeringame[i] ) continue;
				players[i].cheats &= ~CF_TOTALLYFROZEN;
				players[i].mo.bNODAMAGE = false;
			}
			return;
		}
		whom.player.cheats &= ~CF_TOTALLYFROZEN;
		whom.bNODAMAGE = false;
	}

	// gives a player their start items
	// previously a full replica of GiveDefaultInventory, but it's been
	// exported in 3.7.2
	static void GiveStartItems( Actor mo )
	{
		if ( !(mo is 'PlayerPawn') ) return;
		PlayerPawn(mo).GiveDefaultInventory();
		PlayerPawn(mo).BringUpWeapon();
	}

	// RIP demons
	static void KillAllExceptLord( Actor killer )
	{
		let ti = ThinkerIterator.Create("Actor");
		Actor a;
		while ( a = Actor(ti.Next()) )
		{
			if ( !a.bISMONSTER || a.player || a is 'mkNightmareLord' ) continue;
			a.DamageMobj(killer,killer,int.max,'Massacre',DMG_FORCED|DMG_THRUSTLESS);
		}
	}

	// get one part only of full level name (for ACS)
	// 0: name, 1: author
	static clearscope String GetSplitLevelname( int part )
	{
		Array<String> splitname;
		level.levelname.Split(splitname," - by: ");
		return splitname[part];
	}

	// kills all actors in a sector
	static void KillInSector( int tag, bool crushing = true )
	{
		let t = SectorTagIterator.Create(tag);
		for ( int s=t.Next(); s!=-1; s=t.Next() )
			for ( Actor a=level.Sectors[s].thinglist; a; a=a.snext )
				a.DamageMobj(null,null,int.max,crushing?'Crush':'Massacre',DMG_FORCED|DMG_THRUSTLESS);
	}

	static void BathroomMusic( Actor activator, int mode, bool local )
	{
		bool doit = false;
		if ( local )
		{
			if ( activator.player == players[consoleplayer] )
				doit = true;
		}
		else
		{
			for ( int st=22; st<=24; st++ )
			{
				SectorTagIterator t = SectorTagIterator.Create(st);
				for ( int i=t.Next(); i!=-1; i=t.Next() )
				{
					if ( players[consoleplayer].mo.CurSector != level.Sectors[i] ) continue;
					doit = true;
					break;
				}
				if ( doit ) break;
			}
		}
		if ( !doit ) return;
		if ( mode == 0 )
		{
			ShowLine(13,false);
			S_ChangeMusic("D_SPKHUB");
			SetMusicVolume(1.0);
		}
		else if ( mode == 1 )
		{
			ShowLine(13,false);
			S_ChangeMusic("D_SPKHUB");
			SetMusicVolume(0.25);
		}
		else
		{
			ShowLine(13,true);
			S_ChangeMusic("D_MIRROR");
			SetMusicVolume(1.0);
		}
	}

	static void FixPlayerMusic( int bmode )
	{
		// check where the player is located in the hub
		Vector3 cppos = players[consoleplayer].mo.pos;
		if ( (cppos.x > 1024) && (cppos.x < 1472) && (cppos.y > 1664) && (cppos.y < 1920) )
		{
			if ( bmode == 0 )
			{
				ShowLine(13,false);
				S_ChangeMusic("D_SPKHUB");
				SetMusicVolume(1.0);
			}
			else if ( bmode == 1 )
			{
				ShowLine(13,false);
				S_ChangeMusic("D_SPKHUB");
				SetMusicVolume(0.25);
			}
			else
			{
				ShowLine(13,true);
				S_ChangeMusic("D_MIRROR");
				SetMusicVolume(1.0);
			}
		}
		else
		{
			ShowLine(13,false);
			SetMusicVolume(1.0);
			if ( (cppos.x > 3968) && (cppos.x < 4608) && (cppos.y > 3824) && (cppos.y < 5248) )
				S_ChangeMusic("");
			else S_ChangeMusic("D_SPKHUB");
		}
	}

	static bool YellowRed( Actor a, int checktype )
	{
		if ( checktype )
			return a.checkkeys(1,false,true)&&a.checkkeys(4,false,true);
		return a.checkkeys(3,false,true)&&a.checkkeys(6,false,true);
	}
}

// pretty cutting-edge stuff
Class Spook13Dimmer : Thinker
{
	Array<Actor> lights;
	Actor skylight;
	Array<Actor> suns;
	Actor skydim;
	double speed, str, maxstr;
	bool reverse;

	static void Init( double speed, double maxstr, double initstr = 0.0, bool reverse = false )
	{
		let s = new("Spook13Dimmer");
		s.speed = speed/TICRATE;
		if ( reverse )
		{
			s.str = maxstr;
			s.maxstr = initstr;
		}
		else
		{
			s.str = initstr;
			s.maxstr = maxstr;
		}
		s.reverse = reverse;
		s.ChangeStatNum(STAT_USER);
		ActorIterator ai;
		Actor a;
		ai = ActorIterator.Create(313);
		while ( (a = ai.Next()) )
			s.lights.Push(a);
		ai = ActorIterator.Create(314);
		s.skylight = ai.Next();
		ai = ActorIterator.Create(413);
		s.skydim = ai.Next();
		ai = ActorIterator.Create(414);
		while ( (a = ai.Next()) )
			s.suns.Push(a);
	}

	override void Tick()
	{
		Super.Tick();
		if ( reverse ) str = max(str-speed,maxstr);
		else str = min(str+speed,maxstr);
		for ( int i=0; i<lights.Size(); i++ )
		{
			lights[i].args[0] = int(128*(1-str));
			lights[i].args[1] = int(96*(1-str));
			lights[i].args[2] = int(16*(1-str));
		}
		skylight.args[0] = int(255*(1-str));
		skylight.args[1] = int(192*(1-str));
		skylight.args[2] = int(32*(1-str));
		suns[0].alpha = 0.5*(1-str);
		suns[1].alpha = 0.5*(1-str);
		skydim.alpha = str;
		if ( reverse && (str <= maxstr) )
			Destroy();
		if ( !reverse && (str >= maxstr) )
			Destroy();
	}
}

// lite version, only useful for GetAxes
Class st_Matrix4Lite
{
	private double m[16];

	st_Matrix4Lite init()
	{
		int i;
		for ( i=0; i<16; i++ ) m[i] = 0;
		return self;
	}

	static st_Matrix4Lite create()
	{
		return new("st_Matrix4Lite").init();
	}

	static st_Matrix4Lite identity()
	{
		st_Matrix4Lite o = st_Matrix4Lite.create();
		for ( int i=0; i<4; i++ ) o.set(i,i,1);
		return o;
	}

	double get( int c, int r )
	{
		return m[r*4+c];
	}

	void set( int c, int r, double v )
	{
		m[r*4+c] = v;
	}

	st_Matrix4Lite mul( st_Matrix4Lite o )
	{
		st_Matrix4Lite r = st_Matrix4Lite.create();
		int i, j;
		for ( i=0; i<4; i++ ) for ( j=0; j<4; j++ )
			r.set(j,i,get(0,i)*o.get(j,0)+get(1,i)*o.get(j,1)+get(2,i)*o.get(j,2)+get(3,i)*o.get(j,3));
		return r;
	}

	Vector3 vmat( Vector3 o )
	{
		double x, y, z, w;
		x = get(0,0)*o.x+get(1,0)*o.y+get(2,0)*o.z+get(3,0);
		y = get(0,1)*o.x+get(1,1)*o.y+get(2,1)*o.z+get(3,1);
		z = get(0,2)*o.x+get(1,2)*o.y+get(2,2)*o.z+get(3,2);
		w = get(0,3)*o.x+get(1,3)*o.y+get(2,3)*o.z+get(3,3);
		return (x,y,z)/w;
	}

	static st_Matrix4Lite rotate( Vector3 axis, double theta )
	{
		st_Matrix4Lite r = st_Matrix4Lite.identity();
		double s, c, oc;
		s = sin(theta);
		c = cos(theta);
		oc = 1.0-c;
		r.set(0,0,oc*axis.x*axis.x+c);
		r.set(1,0,oc*axis.x*axis.y-axis.z*s);
		r.set(2,0,oc*axis.x*axis.z+axis.y*s);
		r.set(0,1,oc*axis.y*axis.x+axis.z*s);
		r.set(1,1,oc*axis.y*axis.y+c);
		r.set(2,1,oc*axis.y*axis.z-axis.x*s);
		r.set(0,2,oc*axis.z*axis.x-axis.y*s);
		r.set(1,2,oc*axis.z*axis.y+axis.x*s);
		r.set(2,2,oc*axis.z*axis.z+c);
		return r;
	}

	// UE-like axes from rotation
	static Vector3, Vector3, Vector3 getaxes( double pitch, double yaw, double roll )
	{
		Vector3 x = (1,0,0), y = (0,-1,0), z = (0,0,1);	// y inverted for left-handed result
		st_Matrix4Lite mRoll = st_Matrix4Lite.rotate((1,0,0),roll);
		st_Matrix4Lite mPitch = st_Matrix4Lite.rotate((0,1,0),pitch);
		st_Matrix4Lite mYaw = st_Matrix4Lite.rotate((0,0,1),yaw);
		st_Matrix4Lite mRot = mRoll.mul(mYaw);
		mRot = mRot.mul(mPitch);
		x = mRot.vmat(x);
		y = mRot.vmat(y);
		z = mRot.vmat(z);
		return x, y, z;
	}
}
