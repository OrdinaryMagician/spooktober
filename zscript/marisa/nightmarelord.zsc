// The big boss
Class mkDialogueSequence39 : mkDialogueSequence
{
	override void DialogueSeq( int state )
	{
		switch( state )
		{
		case 0:
			SetHeader("$BOSSHDR");
			SetTextEffect(TE_SHIVER);
			SetTextSpeed(3);
			SetVoice("misc/bossvoice");
			SetVoiceVolume(1.);
			SetTextColor(Font.CR_BLACK);
			StartText("$BOSSDLG1");
			break;
		case 1:
			StartText("$BOSSDLG2");
			break;
		case 2:
			StartText("$BOSSDLG3");
			break;
		case 3:
			StartText("$BOSSDLG4");
			break;
		case 4:
			StartText("$BOSSDLG5");
			break;
		case 5:
			EventHandler.SendNetworkEvent("activateboss");
			EndSeq(0);
			break;
		}
	}
}

// Souls-style health bar for a boss
Class mkBossHealthBar : HUDMessageBase
{
	String tname;
	Actor target;
	transient font tfont;
	TextureID bbar_r, bbar_d;
	double alpha;
	DynamicValueInterpolator ihealth;
	int thealth, hmax;
	int oldhealth[10];

	mkBossHealthBar Init( Actor target, String tname )
	{
		self.target = target;
		self.tname = tname;
		alpha = 0.0;
		hmax = thealth = target.health;
		ihealth = DynamicValueInterpolator.Create(thealth,0.25,1,max(8,hmax/100));
		for ( int i=0; i<10; i++ ) oldhealth[i] = thealth;
		bbar_r = TexMan.CheckForTexture("bbar_r",TexMan.Type_Any);
		bbar_d = TexMan.CheckForTexture("bbar_d",TexMan.Type_Any);
		return self;
	}

	override bool Tick()
	{
		if ( target )
		{
			thealth = target.health;
			alpha = min(alpha+0.08,1.0);
		}
		else
		{
			thealth = 0;
			alpha = max(alpha-0.02,0.0);
		}
		oldhealth[0] = thealth;
		if ( thealth > oldhealth[9] )
			for ( int i=9; i>0; i-- )
				oldhealth[i] = thealth;
		ihealth.Update(oldhealth[9]);
		for ( int i=9; i>0; i-- )
			oldhealth[i] = oldhealth[i-1];
		return (!target && (alpha<=0.0));
	}

	override void OnDestroy()
	{
		Super.OnDestroy();
		if ( ihealth ) ihealth.Destroy();
	}

	double GetHealth( bool delay = false )
	{
		double hf;
		if ( delay ) hf = clamp(ihealth.GetValue(),0,hmax)/double(hmax);
		else hf = clamp(thealth,0,hmax)/double(hmax);
		return 256*hf;
	}

	override void Draw( int bottom, int visibility )
	{
		if ( !tfont ) tfont = Font.GetFont('STAT_LWR');
		Vector2 pos, sz;
		[pos, sz] = Screen.VirtualToRealCoords((31,15),(258,8),(320,200),true);
		Screen.Dim("Black",alpha*0.75,int(pos.x),int(pos.y),int(sz.x),int(sz.y));
		Screen.DrawText(tfont,Font.CR_RED,(320-tfont.StringWidth(tname))/2,24,tname,DTA_Bottom320x200,true,DTA_Alpha,alpha);
		Screen.DrawTexture(bbar_d,false,32,16,DTA_Bottom320x200,true,DTA_Alpha,alpha*0.5,DTA_DestWidthF,GetHealth(true));
		Screen.DrawTexture(bbar_r,false,32,16,DTA_Bottom320x200,true,DTA_Alpha,alpha,DTA_DestWidthF,GetHealth());
	}
}

Class StompDust : Actor
{
	Default
	{
		RenderStyle "Translucent";
		Radius 8;
		Height 2;
		Alpha 0.5;
		Scale 1.5;
		Gravity 0.5;
		+NOBLOCKMAP;
		+DONTSPLASH;
		+MISSILE;
		+STEPMISSILE;
		+THRUACTORS;
	}
	States
	{
	Spawn:
		SDST A 1
		{
			A_FadeOut(0.02);
			A_SetScale(scale.x*1.01,scale.y*0.99);
		}
		Wait;
	Death:
		SDST A 1
		{
			A_FadeOut(0.06);
			A_SetScale(scale.x*1.01,scale.y*0.99);
		}
		Wait;
	}
}

Class mkPop : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 5.0;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+DONTSPLASH;
	}
	States
	{
	Spawn:
		FXNL D 1 BRIGHT
		{
			A_SetScale(scale.x-1.0);
			if ( scale.x <= 0.0 ) Destroy();
		}
		Wait;
	}
}

Class mkFireworkLight : PointLightAttenuated
{
	Default
	{
		args 160,0,255,300;
	}
	override void Tick()
	{
		if ( !tracer )
		{
			Destroy();
			return;
		}
		args[LIGHT_RED] = int(160*tracer.alpha);
		args[LIGHT_BLUE] = int(255*tracer.alpha);
		SetOrigin(tracer.pos,true);
	}
}

Class mkFireworks : Actor
{
	double rollvel;
	Default
	{
		RenderStyle "Add";
		Scale 8.0;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+DONTSPLASH;
	}
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		double ang = FRandom[NLord](0,360);
		double pt = FRandom[NLord](-90,90);
		vel = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*FRandom[NLord](0.3,3.5);
		A_PlaySound("nightmarelord/cast");
		rollvel = FRandom[NLord](5,15)*RandomPick[NLord](-1,1);
		let l = Spawn("mkFireworkLight",pos);
		l.tracer = self;
	}
	override void Tick()
	{
		Super.Tick();
		roll += rollvel;
	}
	States
	{
	Spawn:
		FXNL D 2 BRIGHT
		{
			if ( !Random[NLord](0,5) )
			{
				double ang = FRandom[NLord](0,360);
				double pt = FRandom[NLord](-90,90);
				let s = Spawn("mkBigSpeck",pos);
				s.speed *= 8;
				s.vel = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*s.speed;
			}
			A_FadeOut(0.1);
		}
		Wait;
	}
}

Class mkBigSpeck : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 1.5;
		Radius 4;
		Height 2;
		Speed 5;
		ReactionTime 30;
		Species "NightmareLord";
		+THRUSPECIES;
		+MISSILE;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+DONTSPLASH;
	}
	States
	{
	Spawn:
		FXNL D 0 NoDelay A_PlaySound("nightmarelord/spark");
		FXNL DDEE 1 BRIGHT
		{
			Vector3 dir = (FRandom[NLord](-1,1),FRandom[NLord](-1,1),FRandom[NLord](-1,1)).unit();
			vel = (vel.unit()+dir*FRandom[NLord](0.4,0.8)).unit()*speed;
			double ang, pt;
			for ( int i=0; i<2; i++ )
			{
				ang = FRandom[NLord](0,360);
				pt = FRandom[NLord](-90,90);
				Vector3 sof = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*FRandom[NLord](0,4);
				Spawn("mkLilSpeck",Vec3Offset(sof.x,sof.y,sof.z));
			}
			A_CountDown();
		}
		Goto Spawn+1;
	Death:
		FXNL G 0 A_Explode(4,35);
		FXNL GHIJKL 1 BRIGHT;
		Stop;
	}
}

Class mkLilSpeck : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 0.75;
		Radius 4;
		Height 2;
		Speed 1;
		+MISSILE;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+THRUACTORS;
		+DONTSPLASH;
	}
	States
	{
	Spawn:
		FXNL F 1 BRIGHT
		{
			Vector3 dir = (FRandom[NLord](-1,1),FRandom[NLord](-1,1),FRandom[NLord](-1,1)).unit();
			vel += dir*FRandom[NLord](0.1,0.2)*speed;
			A_FadeOut(0.01);
		}
		Wait;
	Death:
		FXNL F 1 A_FadeOut(0.2);
		Wait;
	}
}

Class mkBigBolt : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 2.4;
		Speed 20;
		Radius 12;
		Height 6;
		DamageFunction (Random[NLord](4,6)*9);
		Species "NightmareLord";
		DamageType "Zapped";
		DeathSound "nightmarelord/cast";
		ReactionTime 200;
		PROJECTILE;
		+THRUSPECIES;
		+SEEKERMISSILE;
	}
	override int DoSpecialDamage( Actor target, int damage, Name damagetype )
	{
		target.vel.xy += Vec2To(target).unit()*(2000/target.mass);
		target.vel.z += 8;
		target.A_PlaySound("nightmarelord/sparkhit",CHAN_7);
		if ( target.player ) target.A_SetBlend("White",1.0,10,"Purple",0.0);
		return damage;
	}
	States
	{
	Spawn:
		FXNL AABBCC 2 BRIGHT
		{
			A_SeekerMissile(0,2);
			double ang, pt;
			for ( int i=0; i<4; i++ )
			{
				ang = FRandom[NLord](0,360);
				pt = FRandom[NLord](-90,90);
				Vector3 sof = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*FRandom[NLord](0,16);
				Spawn("mkLilSpeck",Vec3Offset(sof.x,sof.y,sof.z));
			}
			if ( !Random[NLord](0,3) )
			{
				ang = FRandom[NLord](0,360);
				pt = FRandom[NLord](-90,90);
				let s = Spawn("mkBigSpeck",pos);
				s.vel = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*s.speed;
			}
			A_CountDown();
		}
		Loop;
	Death:
		FXNL G 0 A_Explode(GetMissileDamage(0,0)/2,100);
		FXNL GHIJKL 3 BRIGHT
		{
			double ang, pt;
			for ( int i=0; i<2; i++ )
			{
				ang = FRandom[NLord](0,360);
				pt = FRandom[NLord](-90,90);
				let s = Spawn("mkBigSpeck",pos);
				s.vel = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*s.speed;
			}
		}
		Stop;
	}
}

Class mkBigBoltDecoy : mkBigBolt
{
	Default
	{
		DamageFunction (Random[NLord](4,6)*3);
	}
}

Class mkSplitBolt : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 1.8;
		Speed 25;
		Radius 8;
		Height 4;
		DamageFunction (Random[NLord](4,6)*5);
		Species "NightmareLord";
		DamageType "Zapped";
		DeathSound "nightmarelord/cast";
		ReactionTime 100;
		PROJECTILE;
		+THRUSPECIES;
		+SEEKERMISSILE;
	}
	override int DoSpecialDamage( Actor target, int damage, Name damagetype )
	{
		target.vel.xy += Vec2To(target).unit()*(1200/target.mass);
		target.vel.z += 5;
		target.A_PlaySound("nightmarelord/sparkhit",CHAN_7);
		if ( target.player ) target.A_SetBlend("White",0.8,8,"Purple",0.0);
		return damage;
	}
	States
	{
	Spawn:
		FXNL AABBCC 1 BRIGHT
		{
			A_SeekerMissile(0,2);
			double ang, pt;
			for ( int i=0; i<2; i++ )
			{
				ang = FRandom[NLord](0,360);
				pt = FRandom[NLord](-90,90);
				Vector3 sof = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*FRandom[NLord](0,4);
				Spawn("mkLilSpeck",Vec3Offset(sof.x,sof.y,sof.z));
			}
			A_CountDown();
		}
		Loop;
	Death:
		FXNL G 0
		{
			A_Explode(GetMissileDamage(0,0)/2,50);
			double ang, pt;
			for ( int i=0; i<4; i++ )
			{
				ang = FRandom[NLord](0,360);
				pt = FRandom[NLord](-90,90);
				let s = Spawn("mkBigSpeck",pos);
				s.vel = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*s.speed;
			}
		}
		FXNL GHIJKL 2 BRIGHT;
		Stop;
	}
}

Class mkSplitBoltDecoy : mkSplitBolt
{
	Default
	{
		DamageFunction (Random[NLord](4,6)*2);
	}
}

Class mkSmolBolt : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 0.6;
		Speed 30;
		Radius 4;
		Height 2;
		DamageFunction (Random[NLord](4,6)*2);
		Species "NightmareLord";
		DamageType "Zapped";
		DeathSound "nightmarelord/cast";
		ReactionTime 200;
		PROJECTILE;
		+THRUSPECIES;
	}
	override int DoSpecialDamage( Actor target, int damage, Name damagetype )
	{
		target.vel.xy += Vec2To(target).unit()*(800/target.mass);
		target.vel.z += 3;
		target.A_PlaySound("nightmarelord/sparkhit",CHAN_7);
		if ( target.player ) target.A_SetBlend("White",0.5,3,"Purple",0.0);
		return damage;
	}
	States
	{
	Spawn:
		FXNL ABC 1 BRIGHT
		{
			Spawn("mkLilSpeck");
			vel = (vel.unit()+(FRandom[NLord](-0.1,0.1),FRandom[NLord](-0.1,0.1),FRandom[NLord](-0.1,0.1))).unit()*speed;
			A_FaceMovementDirection();
			A_CountDown();
		}
		Loop;
	Death:
		FXNL G 0
		{
			A_Explode(GetMissileDamage(0,0)/2,10);
			double ang, pt;
			if ( !Random[NLord](0,2) )
			{
				ang = FRandom[NLord](0,360);
				pt = FRandom[NLord](-90,90);
				let s = Spawn("mkBigSpeck",pos);
				s.vel = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*s.speed;
			}
		}
		FXNL GHIJKL 1 BRIGHT;
		Stop;
	}
}

Class mkSmolBoltDecoy : mkSmolBolt
{
	Default
	{
		DamageFunction (Random[NLord](1,3)*2);
	}
}

Class mkFallingBolt : Actor
{
	Default
	{
		RenderStyle "Add";
		Alpha 0.0;
		Scale 0.6;
		Speed 30;
		Radius 4;
		Height 2;
		DamageFunction (Random[NLord](4,6)*2);
		Species "NightmareLord";
		DamageType "Zapped";
		DeathSound "nightmarelord/cast";
		PROJECTILE;
		+THRUSPECIES;
	}
	override int DoSpecialDamage( Actor target, int damage, Name damagetype )
	{
		target.vel.xy += Vec2To(target).unit()*(800/target.mass);
		target.vel.z += 3;
		target.A_PlaySound("nightmarelord/sparkhit",CHAN_7);
		if ( target.player ) target.A_SetBlend("White",0.5,3,"Purple",0.0);
		return damage;
	}
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		double ang = FRandom[NLord](0,360);
		vel.xy = (cos(ang),sin(ang))*FRandom[NLord](0,8);
		vel.z = -15;
	}
	States
	{
	Spawn:
		FXNL MNO 1 BRIGHT
		{
			A_FadeIn(0.1);
			Spawn("mkLilSpeck",pos);
			vel.z -= 0.5;
		}
		Loop;
	Death:
		FXNL G 0
		{
			A_Explode(GetMissileDamage(0,0)/2,30);
			double ang, pt;
			if ( !Random[NLord](0,2) )
			{
				ang = FRandom[NLord](0,360);
				pt = FRandom[NLord](-90,90);
				let s = Spawn("mkBigSpeck",pos);
				s.vel = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*s.speed;
			}
		}
		FXNL GHIJKL 1 BRIGHT;
		Stop;
	}
}

Class mkFallingBoltDecoy : Actor
{
	Default
	{
		DamageFunction (Random[NLord](2,4));
	}
}

Class WallHitter : Thinker
{
	Actor victim, instigator;

	static void AppendTo( Actor victim, Actor instigator )
	{
		let w = new("WallHitter");
		w.ChangeStatNum(STAT_USER);
		w.victim = victim;
	}

	override void Tick()
	{
		Super.Tick();
		if ( !victim ) Destroy();
		if ( victim.BlockingLine || victim.BlockingMobj || (victim.pos.z <= victim.floorz) )
		{
			int dmg = Random[NLord](4,6)*5;
			if ( instigator is 'mkNightmareLordDecoy' ) dmg /= 3;
			victim.DamageMobj(instigator,instigator,dmg,'Crash',DMG_THRUSTLESS);
			Destroy();
		}
	}
}

Class mkNightmareLord : Actor
{
	int scnt, dcnt, strafedir;
	int teletic, evadetic, dashtic, raintic;
	Array<Actor> telespots;
	bool bGrabbing;

	override String GetObituary( Actor victim, Actor inflictor, Name mod, bool playerattack )
	{
		if ( mod == 'Stomp' )
			return "%o was stomped away by the Nightmare Lord.";
		else if ( mod == 'Choke' )
			return "%o couldn't break free from the Nightmare Lord's grasp.";
		else if ( mod == 'Throw' )
			return "%o was blown away by the Nightmare Lord.";
		return Obituary;
	}

	override void Activate( Actor activator )
	{
		if ( !InStateSequence(CurState,FindState("Spawn")) ) return;
		mkDialogueHandler.StartDialogueSequence(self,activator,39,false,false);
	}

	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		// construct tele spot array
		let ai = ActorIterator.Create(113);
		Actor a;
		while ( a = ai.Next() ) telespots.Push(a);
	}

	int CountDecoys()
	{
		let ti = ThinkerIterator.Create("mkNightmareLordDecoy");
		int n = 0;
		while ( ti.Next() ) n++;
		return n;
	}

	Actor FindClosestTeleSpot()
	{
		double dist = double.infinity;
		int f = Random[NLord](0,telespots.Size()-1);
		for ( int i=0; i<telespots.Size(); i++ )
		{
			double tdist = telespots[i].Distance2D(target);
			if ( tdist > dist ) continue;
			dist = tdist;
			f = i;
		}
		return telespots[f];
	}

	Actor FindFarthestTeleSpot()
	{
		double dist = 0;
		int f = Random[NLord](0,telespots.Size()-1);
		for ( int i=0; i<telespots.Size(); i++ )
		{
			double tdist = telespots[i].Distance2D(target);
			if ( tdist < dist ) continue;
			dist = tdist;
			f = i;
		}
		return telespots[f];
	}

	void A_DecideChase()
	{
		if ( !target ) return;
		double dist = Distance2D(target);
		if ( (level.totaltime > teletic+200) && !Random[NLord](0,50) && (dist < 800) )
		{
			if ( !Random[NLord](0,3) )
				tracer = FindClosestTeleSpot();
			else tracer = telespots[Random[NLord](0,telespots.Size()-1)];
			score = 0;
			teletic = level.totaltime;
			SetStateLabel("Teleout");
			return;
		}
		if ( (level.totaltime > dashtic+50) && !Random[NLord](0,20) && (dist > 1000) && !LineTrace(AngleTo(target),dist-(radius+target.radius),0,0,32) )
		{
			dashtic = level.totaltime;
			SetStateLabel("Dash");
			return;
		}
	}

	void A_DecideStrafe()
	{
		if ( !target ) return;
		Vector3 sc = level.SphericalCoords(target.pos,pos,(target.angle,target.pitch));
		if ( !Random[NLord](0,30) && target.CheckSight(self) && (sc.z < 600) && (abs(sc.x) < 15) && (abs(sc.y) < 15) )
		{
			double ang = AngleTo(target);
			Vector2 dir[2];
			int sdir[2];
			dir[0] = RotateVector((0,1),ang);
			dir[1] = RotateVector((0,-1),ang);
			sdir[0] = 1;
			sdir[1] = -1;
			strafedir = 0;
			int ca = 1, cb = 0;
			if ( Random[NLord](0,1) )
			{
				ca = 0;
				cb = 1;
			}
			if ( CheckMove(pos.xy+dir[ca]*speed*2) )
				strafedir = sdir[ca];
			else if ( CheckMove(pos.xy+dir[cb]*speed*2) )
				strafedir = sdir[cb];
			if ( strafedir != 0 ) SetStateLabel("Strafe");
		}
	}

	// Spawns decoys on 2 other tele spots
	void A_SpawnDecoys()
	{
		// decoys can't spawn more of themselves
		if ( self is 'mkNightmareLordDecoy' ) return;
		// too many decoys
		if ( CountDecoys() >= 4 ) return;
		Array<Actor> picked;
		picked.Copy(telespots);
		int i;
		for ( i=0; i<picked.Size(); i++ )
		{
			if ( picked[i] != tracer ) continue;
			picked.Delete(i);
			break;
		}
		Actor aspot, bspot;
		i = Random[NLord](0,picked.Size()-1);
		aspot = picked[i];
		picked.Delete(i);
		i = Random[NLord](0,picked.Size()-1);
		bspot = picked[i];
		Actor s;
		s = Spawn("mkNightmareLordDecoy",aspot.pos);
		s.target = target;
		s.master = self;
		s.bSHOOTABLE = false;
		s.SetStateLabel("TeleIn");
		s = Spawn("mkNightmareLordDecoy",bspot.pos);
		s.target = target;
		s.master = self;
		s.bSHOOTABLE = false;
		s.SetStateLabel("TeleIn");
	}

	void A_DashTowards()
	{
		A_FaceTarget(3);
		Vector2 dir = (cos(angle),sin(angle));
		bool moved = TryMove(pos.xy+dir*speed*2,int(maxdropoffheight));
		if ( A_DecideMelee() )
			return;
		if ( !moved )
		{
			SetStateLabel("See");
			return;
		}
		if ( abs(DeltaAngle(angle,AngleTo(target))) > 90 ) dcnt -= 2;
	}

	void A_Strafe()
	{
		A_FaceTarget(5);
		Vector2 dir = RotateVector((0,strafedir),angle);
		bool moved = TryMove(pos.xy+dir*speed*1.5,int(maxdropoffheight));
		if ( !moved )
		{
			SetStateLabel("See");
			return;
		}
	}

	void A_TryEvasion()
	{
		if ( level.totaltime < evadetic+80 ) return;
		if ( !target || Random[NLord](0,9) ) return;
		evadetic = level.totaltime;
		if ( !Random[NLord](0,3) ) score = 1;
		else score = 0;
		tracer = FindFarthestTeleSpot();
		SetStateLabel("Teleout");
	}

	void A_DecideRanged()
	{
		if ( !target ) return;
		double dist = Distance2D(target);
		if ( !Random[NLord](0,4) && A_DecideMelee(true) )
			return;
		if ( (level.totaltime > raintic+400) && !Random[NLord](0,20) )
		{
			SetStateLabel("RainOfSparks");
			return;
		}
		if ( !Random[NLord](0,3) && (dist > 500) )
		{
			SetStateLabel("HomingBolt");
			return;
		}
		if ( !Random[NLord](0,3) )
		{
			SetStateLabel("BoltBarrage");
			return;
		}
		if ( !Random[NLord](0,3) )
		{
			SetStateLabel("DualBolts");
			return;
		}
		SetStateLabel("Dash");
	}

	void A_BigBolt()
	{
		if ( self is 'mkNightmareLordDecoy' )
			A_SpawnProjectile("mkBigBoltDecoy",height*0.6,0,flags:CMF_AIMDIRECTION);
		else
			A_SpawnProjectile("mkBigBolt",height*0.6,0,flags:CMF_AIMDIRECTION);
		A_StaffHit();
	}

	void A_Barrage()
	{
		for ( int i=0; i<4; i++ )
		{
			if ( self is 'mkNightmareLordDecoy' )
				A_SpawnProjectile("mkSmolBoltDecoy",height*0.65,FRandom[NLord](-30,30),FRandom[NLord](-5,5),CMF_AIMDIRECTION,FRandom[NLord](-2,2));
			else
				A_SpawnProjectile("mkSmolBolt",height*0.65,FRandom[NLord](-30,30),FRandom[NLord](-5,5),CMF_AIMDIRECTION,FRandom[NLord](-2,2));
		}
		A_StaffHit();
	}

	void A_DualBolts()
	{
		if ( self is 'mkNightmareLordDecoy' )
		{
			A_SpawnProjectile("mkSplitBoltDecoy",height*0.65,-30,-40,CMF_AIMDIRECTION);
			A_SpawnProjectile("mkSplitBoltDecoy",height*0.65,30,40,CMF_AIMDIRECTION);
		}
		else
		{
			A_SpawnProjectile("mkSplitBolt",height*0.65,-30,-40,CMF_AIMDIRECTION);
			A_SpawnProjectile("mkSplitBolt",height*0.65,30,40,CMF_AIMDIRECTION);
		}
		A_StaffHit();
	}

	void A_RainOfSparks()
	{
		Vector3 originspot;
		for ( int i=0; i<12; i++ )
		{
			originspot = (FRandom[NLord](-1536,1536),FRandom[NLord](-1536,1536),FRandom[NLord](1536,2048));
			Actor s;
			if ( self is 'mkNightmareLordDecoy' )
				s = Spawn("mkFallingBoltDecoy",originspot);
			else
				s = Spawn("mkFallingBolt",originspot);
			s.target = self;
		}
	}

	bool A_DecideMelee( bool ranged = false )
	{
		if ( !target ) return false;
		A_FaceTarget();
		Vector3 sc = level.SphericalCoords(pos,target.pos,(angle,pitch));
		if ( !Random[NLord](0,5) && (sc.z < meleerange*3) )
		{
			SetStateLabel("Grab");
			return true;
		}
		if ( !Random[NLord](0,2) && (sc.z < meleerange*1.5) )
		{
			SetStateLabel("StaffAttack");
			return true;
		}
		if ( (!ranged || !Random[NLord](0,2)) && (sc.z < meleerange*2) )
		{
			SetStateLabel("DoRadiusBlast");
			return true;
		}
		return false;
	}

	void A_SpawnBlast()
	{
		A_PlaySound("nightmarelord/stomp",CHAN_5,1.0,false,0.8);
		A_PlaySound("nightmarelord/stomp",CHAN_6,1.0,false,0.4);
		A_QuakeEx(3,3,3,20,0,600,"",QF_RELATIVE|QF_SCALEDOWN,falloff:300);
		let bi = BlockThingsIterator.Create(self,MeleeRange*2);
		while ( bi.Next() )
		{
			let t = bi.Thing;
			if ( !t || !t.bSHOOTABLE || (t is 'mkNightmareLord') ) continue;
			t.vel.xy = Vec2To(t).unit()*(2500/t.mass);
			t.vel.z = 8;
			int dmg = Random[NLord](3,5)*3;
			if ( self is 'mkNightmareLordDecoy' ) dmg /= 3;
			t.DamageMobj(self,self,dmg,'Stomp',DMG_THRUSTLESS|DMG_EXPLOSION);
		}
		for ( int i=0; i<360; i+=10 )
		{
			let s = Spawn("StompDust",pos);
			s.vel.xy = (cos(i),sin(i))*9.0;
		}
	}

	void A_AttemptGrab()
	{
		if ( !target ) return;
		A_FaceTarget(15);
		Vector3 sc = level.SphericalCoords(pos,target.pos,(angle,pitch));
		if ( (abs(sc.x) < 60) && (sc.z < meleerange*FRandom[NLord](3,6)) )
		{
			tracer = target;
			scnt = Random[NLord](80,120);
			SetStateLabel("Grabbed");
		}
	}

	bool A_KeepGrab()
	{
		if ( !tracer || (tracer.Health <= 0) ) return true;
		bGrabbing = true;
		PainChance = 12;
		if ( !Random[NLord](0,15) ) tracer.DamageMobj(self,self,Random[NLord](1,3),'Choke',DMG_THRUSTLESS);
		Vector2 aimdir = (cos(angle),sin(angle))*(radius+tracer.radius+48);
		Vector3 dest = Vec3Offset(aimdir.x,aimdir.y,height*0.6);
		Vector3 tdir = level.Vec3Diff(tracer.pos,dest);
		double tl = tdir.length();
		if ( tl > double.epsilon ) tdir = tdir.unit()*min(tl,3);
		tracer.vel = tdir;
		tracer.A_SetAngle(tracer.angle+sin(level.totaltime*3.2)*1.2,SPF_INTERPOLATE);
		tracer.A_SetPitch(tracer.pitch+cos(level.totaltime*3.3)*0.6,SPF_INTERPOLATE);
		scnt--;
		return (scnt <= 0);
	}

	void A_Throw()
	{
		if ( !tracer ) return;
		bGrabbing = false;
		A_PlaySound("nightmarelord/grabthrow",CHAN_7);
		tracer.A_PlaySound("nightmarelord/grabthrow",CHAN_7);
		tracer.vel.xy = (cos(angle),sin(angle))*(5000/tracer.mass);
		tracer.vel.z = 16;
		WallHitter.AppendTo(tracer,self);
		int dmg = Random[NLord](3,5)*5;
		if ( self is 'mkNightmareLordDecoy' ) dmg /= 3;
		tracer.DamageMobj(self,self,dmg,'Throw',DMG_THRUSTLESS|DMG_EXPLOSION);
	}

	void A_StaffHit()
	{
		if ( !target ) return;
		Vector3 sc = level.SphericalCoords(pos,target.pos,(angle,pitch));
		if ( (abs(sc.x) < 60) && (sc.z < meleerange*4) )
		{
			target.A_PlaySound("nightmarelord/sparkhit",CHAN_7);
			target.vel.xy = (cos(angle),sin(angle))*(3000/target.mass);
			target.vel.z = 8;
			int dmg = Random[NLord](3,5)*7;
			if ( self is 'mkNightmareLordDecoy' ) dmg /= 3;
			target.DamageMobj(self,self,dmg,'Zapped',DMG_THRUSTLESS);
			if ( target.player ) target.A_SetBlend("White",1.0,15,"Purple",0.0);
		}
	}

	bool CheckFeet()
	{
		bool dummy;
		Actor onmobj;
		[dummy, onmobj] = TestMobjZ(true);
		return (onmobj || pos.z <= floorz);
	}

	// How it'll work
	// Floats upwards for a while, begins emiting sparks and glowing more and more
	// Eventually just pops out of existence with a CORK sfx, only to create a massive,
	// loud blast immediately afterwards
	bool A_Levitate()
	{
		vel.z += 0.01;
		scnt++;
		angle += min(scnt*0.1,20);
		if ( scnt > 100 )
		{
			return true;
		}
		return false;
	}
	bool A_Spark()
	{
		double ang, pt;
		if ( !Random[NLord](0,2) )
		{
			for ( int i=0; i<8; i++ )
			{
				ang = FRandom[NLord](0,360);
				pt = FRandom[NLord](-90,90);
				if ( !Random[NLord](0,9) )
				{
					let s = Spawn("mkBigSpeck",Vec3Offset(FRandom[NLord](-27,27),FRandom[NLord](-27,27),FRandom[NLord](10,130)));
					s.speed *= 4;
					s.vel = (cos(ang)*cos(pt),sin(ang)*cos(pt),-sin(pt))*s.speed;
				}
				else
				{
					let s = Spawn("mkLilSpeck",Vec3Offset(FRandom[NLord](-27,27),FRandom[NLord](-27,27),FRandom[NLord](10,130)));
					s.speed *= 4;
				}
			}
		}
		vel.z = max(vel.z-0.012,0);
		scnt += 2;
		angle += min(scnt*0.1,20);
		return (scnt > 500);
	}
	void A_Pop()
	{
		A_SetRenderStyle(1.0,STYLE_Stencil);
		SetShade("White");
		A_PlaySound("nightmarelord/pop",attenuation:ATTN_NONE);
		scnt = 30;
	}
	void A_Splode()
	{
		for ( int i=0; i<5; i++ )
			Spawn("mkFireworks",Vec3Offset(FRandom[NLord](-27,27),FRandom[NLord](-27,27),FRandom[NLord](10,130)));
		scnt--;
		if ( scnt <= 0 ) Destroy();
	}

	Default
	{
		//$Title Nightmare Lord
		//$Category Marisa Kirisame
		Health 25000;
		Scale 2.0;
		Radius 32;
		Height 140;
		Mass 2000;
		MaxStepHeight 32;
		Painchance 32;
		Speed 13;
		Gravity 3;
		Species "NightmareLord";
		DamageFactor "BFGSplash", 0.25;
		MONSTER;
		+LOOKALLAROUND;
		+QUICKTORETALIATE;
		+NOTELEFRAG;
		+DROPOFF;
		+BOSS;
		+DONTMORPH;
		+NORADIUSDMG;
		+NOTARGET;
		+NOICEDEATH;
		+FLOORCLIP;
		+FULLVOLDEATH;
		+THRUSPECIES;
		+DONTHARMSPECIES;
		+INTERPOLATEANGLES;
		BloodColor "Purple";
		SeeSound "nightmarelord/see";
		PainSound "nightmarelord/pain";
		DeathSound "nightmarelord/death";
		Obituary "%o succumbed to the Nightmare Lord.";
		Tag "$BOSSHDR";
	}
	States
	{
	Spawn:
		NLRD M -1;
		Stop;
	Idle:
		NLRD MMMMMNNNNNN 2 A_Look();
		Loop;
	See:
		NLRD M 0
		{
			scnt = 0;
			PainChance = GetDefaultByType(GetClass()).PainChance;
		}
		NLRD MMNNOOPP 3
		{
			scnt++;
			if ( !(scnt%4) )
				A_PlaySound("nightmarelord/step",CHAN_5);
			A_Chase();
			A_DecideStrafe();
			if ( !Random[NLord](0,16) ) A_DecideMelee();
		}
		NLRD P 0 A_DecideChase();
		Loop;
	Pain:
		NLRD Q 0
		{
			if ( bGrabbing )
			{
				bGrabbing = false;
				vel -= (cos(angle),sin(angle))*12;
			}
			PainChance = GetDefaultByType(GetClass()).PainChance;
		}
		NLRD Q 3 A_Pain();
		NLRD QQ 3 A_TryEvasion();
		Goto See;
	Missile:
		NLRD # 3 A_DecideRanged();
		Goto See;
	HomingBolt:
		NLRD W 5 A_FaceTarget();
		NLRD R 5
		{
			A_FaceTarget(30);
			A_PlaySound("nightmarelord/zap");
		}
		NLRD W 4 A_FaceTarget(30);
		NLRD V 4
		{
			A_FaceTarget(30);
			A_PlaySound("nightmarelord/zap");
		}
		NLRD W 3 A_FaceTarget(30);
		NLRD V 3
		{
			A_FaceTarget(30);
			A_PlaySound("nightmarelord/zap");
		}
		NLRD W 2 A_FaceTarget(30);
		NLRD S 4
		{
			A_FaceTarget(30);
			A_PlaySound("nightmarelord/cast");
		}
		NLRD T 12 A_BigBolt();
		NLRD S 4;
		NLRD W 5;
		NLRD M 5;
		Goto See;
	DualBolts:
		NLRD W 4 A_FaceTarget();
		NLRD R 4
		{
			A_FaceTarget(30);
			A_PlaySound("nightmarelord/zap");
		}
		NLRD W 3 A_FaceTarget(30);
		NLRD V 3
		{
			A_FaceTarget(30);
			A_PlaySound("nightmarelord/zap");
		}
		NLRD W 2 A_FaceTarget(30);
		NLRD S 4
		{
			A_FaceTarget(30);
			A_PlaySound("nightmarelord/cast");
		}
		NLRD T 12 A_DualBolts();
		NLRD S 4;
		NLRD W 5;
		NLRD M 5;
		Goto See;
	BoltBarrage:
		NLRD W 3 A_FaceTarget(30);
		NLRD V 3
		{
			A_FaceTarget(30);
			A_PlaySound("nightmarelord/zap");
		}
		NLRD W 2 A_FaceTarget(30);
		NLRD S 4
		{
			A_FaceTarget(30);
			A_PlaySound("nightmarelord/cast");
		}
		NLRD T 0 { scnt = Random[NLord](10,20); }
	BoltBarrage2:
		NLRD T 4
		{
			A_FaceTarget(10);
			A_Barrage();
			scnt--;
			return A_JumpIf(scnt<=0,1);
		}
		Goto BoltBarrage2;
		NLRD S 4;
		NLRD W 5;
		NLRD M 5;
		Goto See;
	RainOfSparks:
		NLRD G 9
		{
			if ( !(self is 'mkNightmareLordDecoy') )
				A_PlaySound(SeeSound,CHAN_VOICE,1.0,false,ATTN_NONE);
		}
		NLRD G 0
		{
			bSHOOTABLE = false;
			A_PlaySound("nightmarelord/disappear",CHAN_6,1.0,false,0.3);
		}
		NLRD HIJKL 3;
		TNT1 A 30
		{
			bSOLID = false;
			scnt = Random[NLord](90,120);
		}
	RainLoop:
		TNT1 A 4
		{
			A_RainOfSparks();
			scnt--;
			return A_JumpIf(scnt<=0,1);
		}
		Goto RainLoop;
		TNT1 A 90;
		NLRD L 0
		{
			raintic = level.totaltime;
			bSOLID = true;
			tracer = FindFarthestTeleSpot();
			score = 0;
			TeleportMove(tracer.pos,true);
			angle = tracer.angle;
		}
		Goto TeleIn;
	Melee:
		NLRD # 3 A_DecideMelee();
		Goto See;
	DoRadiusBlast:
		NLRD M 4
		{
			if ( !(self is 'mkNightmareLordDecoy') )
				A_PlaySound("nightmarelord/pain3");
		}
		NLRD M 2 { vel.z = 16; }
		NLRD M 1 A_JumpIf(CheckFeet(),1);
		Goto DoRadiusBlast+2;
		NLRD M 25 A_SpawnBlast();
		Goto See;
	Grab:
		NLRD M 15;
		NLRD M 0
		{
			if ( !(self is 'mkNightmareLordDecoy') )
				A_PlaySound("nightmarelord/attack",CHAN_VOICE,1.0,false,ATTN_NONE);
		}
		NLRD UUUUUUUUUU 4 A_AttemptGrab();
		NLRD M 10;
		Goto See;
	Grabbed:
		NLRD U 0
		{
			if ( !Random[NLord](0,4) && !(self is 'mkNightmareLordDecoy') )
				A_PlaySound("nightmarelord/grabtaunt",CHAN_VOICE,1.0,false,ATTN_NONE);
		}
		NLRD U 1 A_JumpIf(A_KeepGrab(),1);
		Wait;
		NLRD U 15 A_Throw();
		NLRD M 10;
		Goto See;
	StaffAttack:
		NLRD W 4 A_FaceTarget(15);
		NLRD R 4
		{
			A_FaceTarget(15);
			A_PlaySound("nightmarelord/zap");
		}
		NLRD W 3 A_FaceTarget(15);
		NLRD V 3
		{
			A_FaceTarget(15);
			A_PlaySound("nightmarelord/zap");
		}
		NLRD W 2 A_FaceTarget(15);
		NLRD S 4
		{
			A_PlaySound("nightmarelord/cast");
			A_FaceTarget(15);
		}
		NLRD T 12 A_StaffHit();
		NLRD S 4;
		NLRD W 5;
		NLRD M 5;
		Goto See;
	Death:
		NLRD Q 30
		{
			A_KillChildren();
			A_NoGravity();
			A_NoBlocking();
			scnt = 0;
		}
		NLRD Q 0 { A_Scream(); A_Stop(); }
		NLRD Q 1 A_JumpIf(A_Levitate(),1);
		Wait;
		NLRD Q 1 A_JumpIf(A_Spark(),1);
		Wait;
		NLRD Q 1
		{
			angle += min(scnt*0.1,20);
			scnt -= 5;
			return A_JumpIf(scnt<=0,1);
		}
		Wait;
		NLRD Q 40;
		NLRD Q 4 BRIGHT A_Pop();
		TNT1 A 100
		{
			Spawn("mkPop",Vec3Offset(0,0,70));
		}
		TNT1 A 0
		{
			A_PlaySound("nightmarelord/explode",CHAN_5,1.0,false,ATTN_NONE);
			A_PlaySound("nightmarelord/explode",CHAN_6,1.0,false,ATTN_NONE);
			A_QuakeEx(6,6,6,200,0,65535,"",QF_RELATIVE|QF_SCALEDOWN);
			players[consoleplayer].mo.A_SetBlend("White",3,100,"Purple",0);
			level.ExecuteSpecial(80,target,null,false,14);
		}
		TNT1 A 5 A_Splode();
		Wait;
	TeleOut:
		NLRD G 9;
		NLRD G 0
		{
			bSHOOTABLE = false;
			A_PlaySound("nightmarelord/disappear",CHAN_6,1.0,false,0.3);
		}
		NLRD HIJKL 3;
		TNT1 A 70 { bSOLID = false; }
		NLRD L 0
		{
			bSOLID = true;
			TeleportMove(tracer.pos,true);
			angle = tracer.angle;
			if ( score > 0 ) A_SpawnDecoys();
		}
	TeleIn:
		NLRD L 0 A_PlaySound("nightmarelord/appear",CHAN_6,1.0,false,0.3);
		NLRD LKJIH 3;
		NLRD G 9
		{
			bSHOOTABLE = true;
			A_FaceTarget();
		}
		Goto See;
	Dash:
		NLRD M 0
		{
			scnt = 0;
			dcnt = Random[NLord](60,40);
			A_FaceTarget();
			A_PlaySound("nightmarelord/dash",CHAN_6,1.0,false,0.3);
		}
		NLRD M 1
		{
			scnt++;
			if ( !(scnt%2) )
			{
				let s = Spawn("mkNAfterImage",pos);
				s.angle = angle;
				s.frame = frame;
			}
			A_DashTowards();
			dcnt--;
			if ( dcnt <= 0 ) return ResolveState("See");
			else return ResolveState(null);
		}
		Goto Dash+1;
	Strafe:
		NLRD M 0
		{
			scnt = 0;
			dcnt = Random[NLord](12,16);
			A_FaceTarget();
			A_PlaySound("nightmarelord/strafe",CHAN_6,1.0,false,0.3);
		}
		NLRD M 1
		{
			scnt++;
			if ( !(scnt%2) )
			{
				let s = Spawn("mkNAfterImage",pos);
				s.angle = angle;
				s.frame = frame;
			}
			A_Strafe();
			dcnt--;
			if ( dcnt <= 0 ) return ResolveState("See");
			else return ResolveState(null);
		}
		Goto Strafe+1;
	}
}

Class mkNAfterImage : Actor
{
	Default
	{
		Scale 2.0;
		RenderStyle "Translucent";
		Alpha 0.5;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+DONTSPLASH;
	}
	States
	{
	Spawn:
		NLRD # 1 A_FadeOut(0.03);
		Wait;
	}
}

// Very fake
// Goes down easily, deals reduced damage and has less attacks
Class mkNightmareLordDecoy : mkNightmareLord
{
	Default
	{
		Health 150;
		Painchance 128;
		SeeSound "";
		PainSound "";
		DeathSound "";
		Species "NightmareLord";
	}
	States
	{
	Death:
		NLRD G 0 A_NoBlocking();
		NLRD GFEDCBA 4
		{
			let s = Spawn("mkNAfterImage",pos);
			s.angle = angle;
			s.frame = frame;
		}
		NLRD A 140;
		NLRD A 1 A_FadeOut();
		Wait;
	}
}

// Skill variations (just different health pools)
Class mkNightmareLordEasy : mkNightmareLord	// ITYTD/HNTR
{
	Default
	{
		Health 20000;
	}
}

Class mkNightmareLordHard : mkNightmareLord	// UV/NM
{
	Default
	{
		Health 30000;
	}
}